<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8" />
    <link rel="icon" href="%PUBLIC_URL%/favicon.ico" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <meta name="theme-color" content="#000000" />
    <meta
      name="description"
      content="Web site created using create-react-app"
    />
    <link rel="apple-touch-icon" href="%PUBLIC_URL%/logo192.png" />
    <link rel="manifest" href="%PUBLIC_URL%/manifest.json" />
    <title>Shaderplay</title>
  </head>

  <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.1/jquery.min.js"></script>

  <!--
    <div id="shaders"></div>
    <script>$('#shaders').load('./shaders.html')</script>
  -->

  <!--
   vertexDefault
  -->
  <script id="vertexDefault" type="x-shader/x-vertex">
    varying vec2 vUv;

    void main()
    {
      vUv = uv;
      gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    }
  </script>

  <!--
   fragmentFireball
  -->
  <script id="fragmentFireball" type="x-shader/x-fragment">
    varying vec2 vUv;
    
    uniform float u_time;
    uniform vec2 u_resolution;
    uniform sampler2D u_texture;
    
    uniform vec2 u_fingers_right[21];
    uniform vec2 u_fingers_left[21];
    uniform vec2 u_hands[2];

    uniform bool u_debug;
    
    uniform float u_size;
    uniform float u_darkness;

    float random (vec2 fc) {
        return fract(sin(dot(fc.xy,
                  vec2(12.9898,78.233))) *
                  43758.5453123);
    }

    void main()
    {
      vec2 fc = gl_FragCoord.xy / u_resolution.xy;

      // Draw debug marks
      if(u_debug)
      {
        float markSize = 0.008;
        float dist = 0.0;
        for(int i=0; i<21; i++)
        {
          dist = distance(fc, u_fingers_right[i]);
          if(dist < markSize)
          {
            gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0) / (dist * 300.0);
            return;
          }
          
          dist = distance(fc, u_fingers_left[i]);
          if(dist < markSize)
          {
            gl_FragColor = vec4(0.0, 0.0, 1.0, 1.0) / (dist * 300.0);
            return;
          }
          
          dist = distance(fc, u_hands[0]);
          if(dist < markSize)
          {
            gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0) / (dist * 300.0);
            return;
          }
          
          dist = distance(fc, u_hands[1]);
          if(dist < markSize)
          {
            gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0) / (dist * 300.0);
            return;
          }
        }
      }

      float noiseLevel = 0.004;
      float tempo = 200.0;
      float border = 160.0;
      float d0 = 1.0, d1 = 1.0;
      float disThresh = 0.008;
      
      if( u_hands[0] != vec2(0.0))
        d0 = distance(fc, u_hands[0]);

      if(u_hands[1] != vec2(0.0))
        d1 = distance(fc, u_hands[1]);
      
      float noise = random(fc);
      float pulse = sin(u_time * tempo * noise) * noise * 0.001;
      float dist = pow(d0 * d1, u_size) + pulse;
      
      if(dist < disThresh)
      {
        gl_FragColor = texture2D(u_texture, vUv)  * (vec4( 0.1, 1.0, 0.1, 1.0) + (dist * border));

      } else
      {
        vec4 color = vec4(vec3(dist) * u_darkness, 1.0);
        gl_FragColor = texture2D(u_texture, vUv) - color;
      }
    }
  </script>

  <!--
   fragmentWhirlpool
  -->
  <script id="fragmentWhirlpool" type="x-shader/x-fragment">
    varying vec2 vUv;
   
    uniform float     u_time;
    uniform sampler2D u_texture;    
    uniform vec2      u_resolution;

    void main()
    {
      vec2 p = - 1.0 + 2.0 * vUv;
      float a = u_time * 40.0;
      float d, e, f, g = 1.0 / 40.0 ,h ,i ,r ,q;

      e = 400.0 * ( p.x * 0.5 + 0.5 );
      f = 400.0 * ( p.y * 0.5 + 0.5 );
      i = 200.0 + sin( e * g + a / 150.0 ) * 20.0;
      d = 200.0 + cos( f * g / 2.0 ) * 18.0 + cos( e * g ) * 7.0;
      r = sqrt( pow( abs( i - e ), 2.0 ) + pow( abs( d - f ), 2.0 ) );
      q = f / r;
      e = ( r * cos( q ) ) - a / 2.0;
      f = ( r * sin( q ) ) - a / 2.0;
      d = sin( e * g ) * 176.0 + sin( e * g ) * 164.0 + r;
      h = ( ( f + d ) + a / 2.0 ) * g;
      i = cos( h + r * p.x / 1.3 ) * ( e + e + a ) + cos( q * g * 6.0 ) * ( r + h / 3.0 );
      h = sin( f * g ) * 144.0 - sin( e * g ) * 212.0 * p.x;
      h = ( h + ( f - e ) * q + sin( r - ( a + h ) / 7.0 ) * 10.0 + i / 4.0 ) * g;
      i += cos( h * 2.3 * sin( a / 350.0 - q ) ) * 184.0 * sin( q - ( r * 4.3 + a / 12.0 ) * g ) + tan( r * g + h ) * 184.0 * cos( r * g + h );
      i = mod( i / 5.6, 256.0 ) / 64.0;
      if ( i < 0.0 ) i += 4.0;
      if ( i >= 2.0 ) i = 4.0 - i;
      d = r / 350.0;
      d += sin( d * d * 8.0 ) * 0.52;
      f = ( sin( a * g ) + 1.0 ) / 2.0;
      
      gl_FragColor = texture2D(u_texture, vUv)
                     + .5 * vec4( vec3( f * i / 1.6, i / 1.0 + d / 3.0, i ) * d * p.x + vec3( i / 1.3 + d / 8.0, i / 1.0 + d / 8.0, i ) * d * ( 1.0 - p.x ), 1.0 );
    }
  </script>

  <!--
   fragmentToon 
  /*
   *  Peter R. Elespuru
   *
   *  modified from:
   *  http://coding-experiments.blogspot.com/search/label/Cel%20Shader
   */
  -->
  <script id="fragmentToon" type="x-shader/x-fragment">
  uniform sampler2D u_texture;
  varying vec2 vUv;

  #define HueLevCount 6
  #define SatLevCount 7
  #define ValLevCount 4

  float HueLevels[HueLevCount];// = float[] (0.0, 80.0, 160.0, 240.0, 320.0, 360.0);
  float SatLevels[SatLevCount];// = float[] (0.0, 0.15, 0.3, 0.45, 0.6, 0.8, 1.0);
  float ValLevels[ValLevCount];// = float[] (0.0, 0.3, 0.6, 1.0);

  vec3 RGBtoHSV( float r, float g, float b) {
     float minv, maxv, delta;
     vec3 res;

     minv = min(min(r, g), b);
     maxv = max(max(r, g), b);
     res.z = maxv;            // v
     
     delta = maxv - minv;

     if( maxv != 0.0 )
        res.y = delta / maxv;      // s
     else {
        // r = g = b = 0      // s = 0, v is undefined
        res.y = 0.0;
        res.x = -1.0;
        return res;
     }

     if( r == maxv )
        res.x = ( g - b ) / delta;      // between yellow & magenta
     else if( g == maxv )
        res.x = 2.0 + ( b - r ) / delta;   // between cyan & yellow
     else
        res.x = 4.0 + ( r - g ) / delta;   // between magenta & cyan

     res.x = res.x * 60.0;            // degrees
     if( res.x < 0.0 )
        res.x = res.x + 360.0;
        
     return res;
  }

  vec3 HSVtoRGB(float h, float s, float v ) {
     int i;
     float f, p, q, t;
     vec3 res;

     if( s == 0.0 ) {
        // achromatic (grey)
        res.x = v;
        res.y = v;
        res.z = v;
        return res;
     }

     h /= 60.0;         // sector 0 to 5
     i = int(floor( h ));
     f = h - float(i);         // factorial part of h
     p = v * ( 1.0 - s );
     q = v * ( 1.0 - s * f );
     t = v * ( 1.0 - s * ( 1.0 - f ) );

     if (i == 0) {
           res.x = v;
           res.y = t;
           res.z = p;
      } else if (i == 1) {
           res.x = q;
           res.y = v;
           res.z = p;
      } else if (i == 2) {
           res.x = p;
           res.y = v;
           res.z = t;
      } else if (i == 3) {
           res.x = p;
           res.y = q;
           res.z = v;
      } else if (i == 4) {
           res.x = t;
           res.y = p;
           res.z = v;
      } else if (i == 5) {
           res.x = v;
           res.y = p;
           res.z = q;
     }
     
     return res;
  }

  float nearestLevel(float col, int mode) {

     int levCount;
     if (mode==0) levCount = HueLevCount;
     if (mode==1) levCount = SatLevCount;
     if (mode==2) levCount = ValLevCount;
     
     for (int i =0; i<levCount-1; i++ ) {
       if (mode==0) {
          if (col >= HueLevels[i] && col <= HueLevels[i+1]) {
            return HueLevels[i+1];
          }
       }
       if (mode==1) {
          if (col >= SatLevels[i] && col <= SatLevels[i+1]) {
            return SatLevels[i+1];
          }
       }
       if (mode==2) {
          if (col >= ValLevels[i] && col <= ValLevels[i+1]) {
            return ValLevels[i+1];
          }
       }
     }
  }

  // averaged pixel intensity from 3 color channels
  float avg_intensity(vec4 pix) {
   return (pix.r + pix.g + pix.b)/3.;
  }

  vec4 get_pixel(vec2 coords, float dx, float dy) {
   return texture2D(u_texture,coords + vec2(dx, dy));
  }

  // returns pixel color
  float IsEdge(in vec2 coords){
    float dxtex = 1.0 / 600.0;//float(textureSize(u_texture,0)) ;
    float dytex = 1.0 / 600.0;//float(textureSize(u_texture,0));
    float pix[9];
    int k = -1;
    float delta;

    // read neighboring pixel intensities
    for (int i=-1; i<2; i++) {
     for(int j=-1; j<2; j++) {
      k++;
      pix[k] = avg_intensity(get_pixel(coords,float(i)*dxtex,
                                            float(j)*dytex));
     }
    }

    // average color differences around neighboring pixels
    delta = (abs(pix[1]-pix[7])+
            abs(pix[5]-pix[3]) +
            abs(pix[0]-pix[8])+
            abs(pix[2]-pix[6])
             )/4.;

    return clamp(5.5*delta,0.0,1.0);
  }

  void main(void)
  {
    HueLevels[0] = 0.0;
    HueLevels[1] = 80.0;
    HueLevels[2] = 160.0;
    HueLevels[3] = 240.0;
    HueLevels[4] = 320.0;
    HueLevels[5] = 360.0;

    SatLevels[0] = 0.0;
    SatLevels[1] = 0.15;
    SatLevels[2] = 0.30;
    SatLevels[3] = 0.45;
    SatLevels[4] = 0.60;
    SatLevels[5] = 0.80;
    SatLevels[6] = 1.0;
    
    ValLevels[0] = 0.0;
    ValLevels[1] = 0.3;
    ValLevels[2] = 0.6;
    ValLevels[3] = 1.0;
    
    vec4 colorOrg = texture2D( u_texture, vUv );
    vec3 vHSV =  RGBtoHSV(colorOrg.r,colorOrg.g,colorOrg.b);
    vHSV.x = nearestLevel(vHSV.x, 0);
    vHSV.y = nearestLevel(vHSV.y, 1);
    vHSV.z = nearestLevel(vHSV.z, 2);
    float edg = IsEdge(vUv);
    vec3 vRGB = (edg >= 0.3)? vec3(0.0,0.0,0.0):HSVtoRGB(vHSV.x,vHSV.y,vHSV.z);
    gl_FragColor = vec4(vRGB.x,vRGB.y,vRGB.z,1.0);
  }
  </script>

<!--
   fragmentPosterize
  /*
   *
   *  Peter R. Elespuru
   *
   *  common approach to posterization
   */
  -->
  <script id="fragmentPosterize" type="x-shader/x-fragment">
  
  uniform sampler2D u_texture;
  varying vec2 vUv;
  
  void main() {
    
    float gamma = 0.4;
    float numColors = 12.0;
    
    vec3 c = texture2D(u_texture, vUv.xy).rgb;
    c = pow(c, vec3(gamma, gamma, gamma));
    c = c * numColors;
    c = floor(c);
    c = c / numColors;
    c = pow(c, vec3(1.0/gamma));
    gl_FragColor = vec4(c, 1.0);
  }
  </script>

  <!--
   fragmentCrosshatch
  /*
   *
   *  Peter R. Elespuru
   *
   *	modified from this idea:
   *	http://learningwebgl.com/blog/?p=2858
   *
   */
  -->
  <script id="fragmentCrosshatch" type="x-shader/x-fragment">
    varying vec2 vUv;
    uniform sampler2D u_texture;

    void main()
    {
      float lum = length(texture2D(u_texture, vUv).rgb);
      
      gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
      
      if (lum < 1.00) {
        if (mod(gl_FragCoord.x + gl_FragCoord.y, 10.0) == 0.0) {
          gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        }
      }
      
      if (lum < 0.75) {
        if (mod(gl_FragCoord.x - gl_FragCoord.y, 10.0) == 0.0) {
          gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        }
      }
      
      if (lum < 0.50) {
        if (mod(gl_FragCoord.x + gl_FragCoord.y - 5.0, 10.0) == 0.0) {
          gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        }
      }
      
      if (lum < 0.3) {
        if (mod(gl_FragCoord.x - gl_FragCoord.y - 5.0, 10.0) == 0.0) {
          gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        }
      }
    }
  </script>

  <!--
   fragmentHoneycombs
  /*
   *
   *  Peter R. Elespuru
   *  
   *  Derived from:
   *  http://coding-experiments.blogspot.com/2010/06/pixelation.html 
   */
  -->
  <script id="fragmentHoneycombs" type="x-shader/x-fragment">
    varying vec2 vUv;
    uniform sampler2D u_texture;
    uniform float u_size;

    vec2 hexCoord(ivec2 hexIndex)
    {
      float S = ((3./2.) * u_size/sqrt(3.));
        
      int i = int(hexIndex.x);
      int j = int(hexIndex.y);
      vec2 r = vec2(0,0);
      r.x = float(i) * S;
      r.y = float(j) * u_size + float(i%2) * u_size/2.;
      return r;
    }

    ivec2 hexIndex(vec2 coord)
    {
      float S = ((3./2.) * u_size/sqrt(3.));
      
      ivec2 r;
      float x = coord.x;
      float y = coord.y;
      int it = int(floor(x/S));
      float yts = y - float(it%2) * u_size/2.;
      int jt = int(floor((1./u_size) * yts));
      float xt = x - float(it) * S;
      float yt = yts - float(jt) * u_size;
      int deltaj = (yt > u_size/2.)? 1:0;
      float fcond = S * (2./3.) * abs(0.5 - yt/u_size);

      if (xt > fcond)
      {
        r.x = it;
        r.y = jt;
      } else
      {
        r.x = it - 1;
        r.y = jt - (r.x%2) + deltaj;
      }

      return r;
    }

    void main(void)
    {
      vec2 xy = vUv;
      ivec2 hexIx = hexIndex(xy);
      vec2 hexXy = hexCoord(hexIx);
      vec4 fcol = texture2D(u_texture, hexXy);
      gl_FragColor = fcol;
    }
  </script>

  <!--
   fragmentPix
  /*
   *
   *  Peter R. Elespuru
   *
   *  modified from:
   *  http://www.geeks3d.com/20101029/shader-library-pixelation-post-processing-effect-glsl/
   */
  -->
  <script id="fragmentPix" type="x-shader/x-fragment">
	 
  uniform sampler2D u_texture;
  varying vec2 vUv;

  uniform float u_size_x;
  uniform float u_size_y;
  
  void main() {
    
   float dx = 5.*(1./u_size_x);
   float dy = 3.*(1./u_size_y);
   
   vec2 coord = vec2(dx*floor(vUv.x/dx),
                     dy*floor(vUv.y/dy));
   
   gl_FragColor = texture2D(u_texture, coord);

  }
  </script>


<!--
 fragmentLine
/*
 *
 *  Peter R. Elespuru
 * 
 *  modified from: 
 *  http://www.forceflow.be/thesis/thesis-code/
 */ 
-->
  <script id="fragmentLine" type="x-shader/x-fragment">
    varying vec2 vUv;
    uniform sampler2D u_texture;

    float intensity(in vec4 color)
    {
      return sqrt((color.x*color.x)+(color.y*color.y)+(color.z*color.z));
    }

    vec3 sobel(float step, vec2 center)
    {
      float limit = 0.2;
      int width = 600;
      
      // get samples around pixel
        float tleft = intensity(texture2D(u_texture,center + vec2(-step,step)));
        float left = intensity(texture2D(u_texture,center + vec2(-step,0)));
        float bleft = intensity(texture2D(u_texture,center + vec2(-step,-step)));
        float top = intensity(texture2D(u_texture,center + vec2(0,step)));
        float bottom = intensity(texture2D(u_texture,center + vec2(0,-step)));
        float tright = intensity(texture2D(u_texture,center + vec2(step,step)));
        float right = intensity(texture2D(u_texture,center + vec2(step,0)));
        float bright = intensity(texture2D(u_texture,center + vec2(step,-step)));

      // Sobel masks (to estimate gradient)
      //        1 0 -1     -1 -2 -1
      //    X = 2 0 -2  Y = 0  0  0
      //        1 0 -1      1  2  1

      float x = tleft + 2.0*left + bleft - tright - 2.0*right - bright;
      float y = -tleft - 2.0*top - tright + bleft + 2.0 * bottom + bright;
        float color = sqrt((x*x) + (y*y));
        if (color > limit){return vec3(0.0,0.0,0.0);}
        return vec3(1.0,1.0,1.0);
     }

    void main(void)
    {
      gl_FragColor = vec4(1,1,1,0);
      float step = 1.0/512.0;
      vec2 center = vUv;
      gl_FragColor.xyz = sobel(step,center);
    }
  </script>

  <!--
   fragmentSobel

  // Sobel edge detection
  //    -1 -2 -1       1 0 -1
  // H = 0  0  0   V = 2 0 -2
  //     1  2  1       1 0 -1
  //
  // result = sqrt(H^2 + V^2)
  -->
  <script id="fragmentSobel" type="x-shader/x-fragment">
  varying vec2 vUv;
  
  uniform sampler2D u_texture;
  uniform float u_dX;
  uniform float u_dY;

  vec4 get_sample(float dx, float dy)
  {
     return texture2D(u_texture, vUv + vec2(dx, dy));
  }

  void main()
  {
    vec4 H = -get_sample(-u_dX,+u_dY) - 2.0*get_sample(0.0,+u_dY) - get_sample(+u_dX,+u_dY)
             +get_sample(-u_dX,-u_dY) + 2.0*get_sample(0.0,-u_dY) + get_sample(+u_dX,-u_dY);

    vec4 V =    get_sample(-u_dX,+u_dY)  -     get_sample(+u_dX,+u_dY)
          + 2.0*get_sample(-u_dX,0.0)  - 2.0*get_sample(+u_dX,0.0)
          +     get_sample(-u_dX,-u_dY)  -     get_sample(+u_dX,-u_dY);

    gl_FragColor = sqrt(H*H + V*V);
  }
  </script>

  <!--
   fragmentGobelin
  /*
   *
   *  Peter R. Elespuru
   *	modified from this idea:
   *	http://learningwebgl.com/blog/?p=2858
   *
   */
  -->
  <script id="fragmentGobelin" type="x-shader/x-fragment">

  varying vec2 vUv;
  uniform sampler2D u_texture;

  vec4 PostFX(sampler2D tex, vec2 uv) {
    
    float stitching_size = 6.0;
    int invert = 0;
    float dim = 600.0;
    
    vec4 c = vec4(0.0);
    float size = stitching_size;
    vec2 cPos = uv * vec2(dim, dim);
    vec2 tlPos = floor(cPos / vec2(size, size));
    tlPos *= size;
    int remX = int(mod(cPos.x, size));
    int remY = int(mod(cPos.y, size));
    if (remX == 0 && remY == 0)
    tlPos = cPos;
    vec2 blPos = tlPos;
    blPos.y += (size - 1.0);
    
    if ((remX == remY) ||
    (((int(cPos.x) - int(blPos.x)) == (int(blPos.y) - int(cPos.y)))))
    {
      if (invert == 1)
      c = vec4(0.2, 0.15, 0.05, 1.0);
      else
      c = texture2D(tex, tlPos * vec2(1.0/dim, 1.0/dim)) * 1.4;
    }
    else
    {
      if (invert == 1)
      c = texture2D(tex, tlPos * vec2(1.0/dim, 1.0/dim)) * 1.4;
      else
      c = vec4(0.0, 0.0, 0.0, 1.0);
    }
    return c;
  }

  void main (void)
  {
    vec2 uv = vUv.st;
    gl_FragColor = PostFX(u_texture, vUv);
    return;
    
    if (uv.y > 0.5)
    {
      gl_FragColor = PostFX(u_texture, vUv);
    }
    else
    {
      uv.y += 0.5;
      vec4 c1 = texture2D(u_texture, vUv);
      gl_FragColor = c1;
    }
  }
  </script>


  <body>
    <noscript>You need to enable JavaScript to run this app.</noscript>
    <video id="video" class="input_video" style="display:none; visibility: hidden;"></video>
    <div id="root"></div>
  </body>

</html>
